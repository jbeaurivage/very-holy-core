/// True Dual-port Block RAM.
/// Ports A and B can be read and written. Supports
/// individual byte writes.
///
/// Memory is word-addressed. Use write_byte_enable
/// to enable write to individual bytes.
pub module DualPortBlockRam #(
    /// Initialize memory with hex file if not empty
    param INIT_FILE: string = "",
    /// Number of columns per entry. Default 4 = 4 bytes/word
    param NUM_COL: u8 = 4,
    /// Number of bits per column. Default 8 bits = 1 byte
    param COL_WIDTH: u8 = 8,
    /// Number of entries in RAM. Default 1024 = 1024 32-bit words
    param RAM_DEPTH : u32 = 1024             ,
    param ADDR_WIDTH: u32 = $clog2(RAM_DEPTH),
) (
    clk: input clock,

    port_a: modport BramPort::slave,
    port_b: modport BramPort::slave,
) {
    const DATA_WIDTH: u8                            = NUM_COL * COL_WIDTH;
    var mem       : logic<DATA_WIDTH> [RAM_DEPTH];

    initial {
        if INIT_FILE != "" {
            $readmemh(INIT_FILE, mem);
        }
    }

    always_ff {
        if port_a.enable {
            for i: u32 in 0..NUM_COL {
                if port_a.byte_write_enable[i] {
                    mem[port_a.address][(i * COL_WIDTH)+:COL_WIDTH] = port_a.write_data[(i * COL_WIDTH)+:COL_WIDTH];
                }
            }
            port_a.read_data = mem[port_a.address];
        }
    }

    always_ff {
        if port_b.enable {
            for i: u32 in 0..NUM_COL {
                if port_b.byte_write_enable[i] {
                    mem[port_b.address][(i * COL_WIDTH)+:COL_WIDTH] = port_b.write_data[(i * COL_WIDTH)+:COL_WIDTH];
                }
            }
            port_b.read_data = mem[port_b.address];
        }
    }
}
